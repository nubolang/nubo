import db from "./db.nb"

event guess(letter: char)
let views = 0

server {
    let word = session("word", fn() => db.getWord())
    let guessed = session("guessed", fn() => [])
    let streak = session("streak", fn() => 0)
    let attempts = session("attempts", fn() => 5)

    views++

    sub guess(letter: char) {
        if attempts <= 0 || guessed.includes(letter) {
            return
        }

        guessed.push(letter)

        if word.includes(letter) {
            streak += 1
        } else {
            streak = 0
            attempts -= 1
        }
    }

    return App(word, guessed, attempts, streak)
}

client App(word, guessed, attempts, streak) {
    let letters = "abcdefghijklmnopqrstuvwxyz".split('')

    fn handleClick(letter: char) {
        pub guess(letter)
    }

    fn displayWord() {
        return word.split('').map(l =>
            guessed.includes(l) ? l : "_"
        ).join(" ")
    }

    return <div>
        <h1>{displayWord()}</h1>
        <p>Streak: {streak}</p>
        <p>Attempts left: {attempts}</p>
        <section>
            { letters.map(letter =>
                <button :click="fn() => handleClick(letter)" :disabled="guessed.includes(letter)">
                    {letter}
                </button>
            ) }
        </section>
    </div>
}
